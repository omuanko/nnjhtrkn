;
;
;	<NINJA HATTORI KUN> music sub
;			by ita
;
MAXMUS	equ	9
MAXEFF	equ	$0f
;
;
MUSINI:	LDA	#0
	STA	MUSCMD
MUSRET:	RTS
;
MUSBRK:	BMI	MUSRET
	ORA	#$80
	STA	PAUSE
	LDA	#$10
	STA	$4000
	STA	$4004
	LDA	#$00
	STA	$4008
	RTS
;
;
MUSIC:
	LDA	PAUSE
	BNE	MUSBRK
	LDA	MUSCMD
	BEQ	MUSRET
	BMI	PLAYMS
;
	CMP	#(MAXMUS+1)
	BCS	MUSINI
	STA	MUSDAT
	ORA	#$80
	STA	MUSCMD
	DEC	MUSDAT
	LDA	MUSDAT
	ASL	A
	ASL	A
	ASL	A
	TAY
	LDX	#0
TRMADL:	LDA	DATATP,Y
	STA	MUSTOP,X
	INY
	INX
	CPX	#6
	BNE	TRMADL
	LDA	DATATP,Y
	STA	MATT
	LDA	DATATP+1,Y
	STA	MATT+1
	LDA	#0
	STA	MUSPTR
	STA	MUSPTR+1
	STA	MUSPTR+2
	STA	MATT+2
	STA	MTAI
	STA	MTAI+1
	STA	MTAI+2
	LDA	#1
	STA	MUSCNT
	STA	MUSCNT+1
	STA	MUSCNT+2
	STA	MUSLEN
	STA	MUSLEN+1
	STA	MUSLEN+2
	STA	MLEN
	STA	MLEN+1
	STA	MLEN+2
	LDA	#$08
	STA	MSWP
	STA	MSWP+1
;
PLAYMS:	LDA	#2
	STA	CHCODE
PLAYLP:	LDX	CHCODE
	DEC	MUSCNT,X
	BEQ	MUSNXT
NEXTCH:	DEC	CHCODE
	BPL	PLAYLP
PLAYRT:	RTS
;
MUSNXT:
	TXA
	ASL	A
	TAX
	LDA	MUSTOP,X
	STA	MUSADR
	LDA	MUSTOP+1,X
	STA	MUSADR+1
	JSR	MUPCOM
	JMP	NEXTCH
;
;
;
MUPCOM:	LDX	CHCODE
	LDY	MUSPTR,X
	LDA	[MUSADR],Y
	STA	MUSDAT
	INC	MUSPTR,X
;
	LDA	MUSDAT
	BPL	WRTFRQ
;
	AND	#$F0
	CMP	#$F0
	BEQ	MUPCO1
;
	LDA	MUSDAT
	AND	#$7F
	STA	MUSLEN,X
	CPX	#2
	BEQ	MLCCH
	LSR	A
	LSR	A
	CMP	#16
	BCC	MUPSL
	LDA	#15
	BNE	MUPSL
MLCCH:	ASL	A
	BPL	MUPSL
	LDA	#$7F
MUPSL:	STA	MLEN,X
	JMP	MUPCOM
;
;
;
MUPCO1:	SEC
	LDA	#$FF
	SBC	MUSDAT
	ASL	A
	TAY
	LDA	MUPJTB+1,Y
	PHA
	LDA	MUPJTB,Y
	PHA
	RTS
;
MUPJTB:	DW	MPEND-1
	DW	MPDC-1
	DW	MPRPB-1
	DW	MPRPE-1
	DW	MPTAI-1
	DW	MPTAIE-1
;
MPEND:	LDA	#0
	STA	MUSCMD
	RTS
;
MPDC:	LDA	#0
	STA	MUSPTR,X
	JMP	MUPCOM
;
MPRPB:	LDY	MUSPTR,X
	LDA	[MUSADR],Y
	STA	RPTTIM,X
	INY
	STY	MUSPTR,X
	STY	RPTPTR,X
	JMP	MUPCOM
;
MPRPE:	DEC	RPTTIM,X
	BEQ	MPRPEE
	LDA	RPTPTR,X
	STA	MUSPTR,X
MPRPEE:	JMP	MUPCOM
;
MPTAI:	LDA	MTAI,X
	BEQ	MPTAI1
	LDA	#$02
	STA	MTAI,X
	JMP	MUPCOM
MPTAI1:	LDA	#$01
	STA	MTAI,X
	JMP	MUPCOM
;
MPTAIE:	LDA	#$FF
	STA	MTAI,X
	JMP	MUPCOM
;
;
;
;	WRITE FREQ
;X=ch.No.
;A=FREQ. CODE
;
WRTFRQ:	LDY	MUSLEN,X
	STY	MUSCNT,X
	LDY	EFFCNT,X
	BNE	WRTFRT
	CMP	#0
	BEQ	WRTFRT
	PHA
	TXA
	ASL	A
	ASL	A
	TAY
	LDA	MTAI,X
	BEQ	WRTF2
	BPL	WFTAI
	INC	MTAI,X
	BEQ	WRTF2	;ALWAYS "EQ"
WFTAI:	CPX	#2
	BEQ	WFTAI2
	LDA	MATT,X
	AND	#$C0
	ORA	#$1F
	BNE	WRTF3	;ALWAYS "NE"
WFTAI2:	LDA	#$7F
	BNE	WRTF3	;ALWAYS "NE"
WRTF2:	LDA	MLEN,X
	ORA	MATT,X
WRTF3:	STA	$4000,Y
	LDA	MTAI,X
	CMP	#2
	BNE	WRTF4
	PLA	;ADJUST STACK
	RTS
WRTF4:	CPX	#2
	BCS	NOSWP
	LDA	MSWP,X
	STA	$4001,Y
NOSWP:	PLA
	ASL	A
	TAX
	LDA	FRQTBL,X
	STA	$4002,Y
	LDA	FRQTBL+1,X
	ORA	#$08
	STA	$4003,Y
WRTFRT:	RTS
;
;
effset:
	pha
	lda	selnfl
	ora	naksfl
	bne	effsrt
	lda	mandir
	and	#$80
	bne	effsrt
	pla
	sta	effcmd
	rts
effsrt:	pla
	rts
;
;
;----------------------------------------------------------------------
;
SOAENV	EQU	$4000
SOASWP	EQU	$4001
SOAFLO	EQU	$4002
SOAFHI	EQU	$4003
SOBENV	EQU	$4004
SOBSWP	EQU	$4005
SOBFLO	EQU	$4006
SOBFHI	EQU	$4007
SOCLEN	EQU	$4008
SOCFLO	EQU	$400A
SOCFHI	EQU	$400B
SONENV	EQU	$400C
SONCLK	EQU	$400E
SONLEN	EQU	$400F
DMCLK	EQU	$4010
DMDAC	EQU	$4011
DMADR	EQU	$4012
DMLEN	EQU	$4013
SOGATE	EQU	$4015
;
SOURES:	LDA	#$00
	STA	DMDAC
	STA	EFFCNT
	STA	EFFCNT+1
	STA	EFFCNT+2
	STA	SOAENV
	STA	SOBENV
	STA	SOCLEN
	STA	SONENV
	LDA	#$0F
	STA	SOGATE
	RTS
;
EFFECT:	LDA	EFFCNT
	ORA	EFFCNT+1
	ORA	EFFCNT+2
	BEQ	EFFC0
;
	LDX	#2
EFFCL:	LDA	EFFCNT,X
	BEQ	EFFC1
	DEC	EFFCNT,X
EFFC1:	DEX
	BPL	EFFCL
;
	LDA	EFFCNT
	ORA	EFFCNT+1
	ORA	EFFCNT+2
	BNE	EFFC0
	LDA	EFFCMD
	BPL	EFFC9
	LDA	#$80
	STA	EFFCMD
	RTS
;
EFFC0:	LDA	EFFCMD
	BMI	EFF1
EFFC9:	CMP	#(MAXEFF+1)
	BCS	EFFRET
	STA	EFFCOD
	ORA	#$80
	STA	EFFCMD
	LDA	#0
	STA	EFFCNT
	STA	EFFCNT+1
	STA	EFFCNT+2
	LDA	EFFCOD
	ASL	A
	TAX
	LDA	EFSJTB+1,X
	PHA
	LDA	EFSJTB,X
	PHA
	RTS
;
EFF1:	LDA	EFFCOD
	CMP	#(MAXEFF+1)
	BCS	EFFRET
	ASL	A
	TAX
	LDA	EFFJTB+1,X
	PHA
	LDA	EFFJTB,X
	PHA
EFFRET:	RTS
;
EFSJTB:	DW	SOURES-1
	DW	EFS01-1		;MONO WO TOTTA
	DW	EFS02-1		;MIZU NI OTITA
	DW	EFS03-1		;SHURIKEN WO NAGETA
	DW	EFS04-1		;TEKI GA SHINDA
	DW	EFS05-1		;ROBONIN NI TAMA GA ATATTA
	DW	EFS06-1		;NINPOU JIKKOU
	DW	EFS07-1		;YARARETA
	DW	EFS08-1		;BONUS COUNT
	DW	EFS09-1		;NINPOU SENTAKU
	DW	EFS0A-1		;JUMON
	DW	EFS0B-1		;SINZO NO NAKIGOE
	DW	EFS0C-1		;JUMP
	DW	EFS0D-1		;HITORI FUETA
	DW	EFS0E-1		;PAUSE
	DW	EFS0F-1		;FALL
;
EFFJTB:	DW	EFFRET-1
	DW	EFF01-1
	DW	EFF02-1
	DW	EFFRET-1
	DW	EFF04-1
	DW	EFF05-1
	DW	EFF06-1
	DW	EFF07-1
	DW	EFFRET-1
	DW	EFFRET-1
	DW	EFF0A-1
	DW	EFF0B-1
	DW	EFFRET-1
	DW	EFF0D-1
	DW	EFF0E-1
	DW	EFF0F-1
;
;
;
EFS01:	LDA	#255
	STA	EFFCNT+1
	LDA	#0
	STA	EFFW1
	LDA	#4
	STA	EFFW2
;
EFF01:	LDA	EFFW1
	BNE	EFF01A
	LDA	EFFW2
	BNE	EFF01B
	LDA	#0
	STA	EFFCOD
	LDA	#1
	STA	EFFCNT+1
	RTS
EFF01B:	DEC	EFFW2
	LDA	#$C4
	STA	SOBENV
	LDA	#$84
	STA	SOBSWP
	LDX	EFFW2
	LDA	EF01D1,X
	STA	SOBFLO
	LDA	#$10
	STA	SOBFHI
	LDA	#4
	STA	EFFW1
EFF01A:	DEC	EFFW1
	RTS
;
EF01D1:	DB	101,135,180,240
;
;
;
EFS02:	LDA	#$04
	STA	SONENV
	LDA	#8
	STA	SONCLK
	LDA	#$08
	STA	SONLEN
	STA	EFFW1
	RTS
;
EFF02:	DEC	EFFW1
	BNE	EFF02A
	LDA	#$0F
	STA	SONENV
	LDA	#4
	STA	SONCLK
	LDA	#$08
	STA	SONLEN
	LDA	#0
	STA	EFFCOD
EFF02A:	RTS
;
;
;
EFS03:	LDA	#8
	STA	EFFCNT+1
	LDA	#$1C
	STA	SONENV
	LDA	#$03
	STA	SONCLK
	LDA	#$48
	STA	SONLEN
	LDA	#$1F
	STA	SOBENV
	LDA	#$8B
	STA	SOBSWP
	LDA	#$40
	STA	SOBFLO
	STA	SOBFHI
	LDA	#0
	STA	EFFCOD
	RTS
;
;
;
EFS04:	LDA	#$10
	STA	EFFCNT+1
	LDA	#$FF
	STA	SOBENV
	LDA	#$84
	STA	SOBSWP
	LDA	#120
	STA	SOBFLO
	LDA	#0
	STA	SOBFHI
	LDA	#4
	STA	EFFW1
	RTS
;
EFF04:	DEC	EFFW1
	BNE	EFS04A
	LDA	#$DF
	STA	SOBENV
	LDA	#$84
	STA	SOBSWP
	LDA	#60
	STA	SOBFLO
	LDA	#$80
	STA	SOBFHI
	LDA	#0
	STA	EFFCOD
EFS04A:	RTS
;
;
;
EFS05:	LDA	#255
	STA	EFFCNT+1
	LDA	#15
	STA	EFFW1
	BNE	EFF05B
;
EFF05:	DEC	EFFW1
	BEQ	EFF05A
EFF05B:	LDA	EFFW1
	ORA	#$10
	STA	SOBENV
	LDA	#$00
	STA	SOBSWP
	LDA	EFFW1
	AND	#1
	ASL	A
	ASL	A
	ASL	A
	ASL	A
	ADC	#$30
	STA	SOBFLO
	LDA	#$10
	STA	SOBFHI
	RTS
EFF05A:	LDA	#$00
	STA	EFFCOD
	LDA	#1
	STA	EFFCNT+1
	RTS
;
;
;
EFS06:	LDA	#255
	STA	EFFCNT+1
	LDA	#$3F
	STA	EFFW1
	BNE	EFF06B
;
EFF06:	DEC	EFFW1
	BMI	EFF06A
EFF06B:	LDA	EFFW1
	LSR	A
	LSR	A
	ORA	#$10
	STA	SOBENV
	STA	SONENV
	LDA	EFFW1
	LSR	A
	LSR	A
	STA	SONCLK
	LDA	EFFW1
	TAX
	AND	#1
	BEQ	EFF06C
	LDA	EFFW1
	ASL	A
	TAX
EFF06C:	TXA
	STA	SOBFLO
	LDA	#$00
	STA	SOBSWP
	LDA	#$10
	STA	SOBFHI
	STA	SONLEN
	RTS
EFF06A:	LDA	#0
	STA	EFFCOD
	LDA	#1
	STA	EFFCNT+1
	RTS
;
;
;
EFS07:	LDA	#20
	STA	EFFW1
	LDA	#0
	STA	EFFW2
	LDA	#255
	STA	EFFCNT+1
;
EFF07:	LDX	EFFW1
	LDA	EF07DT-1,X
	BNE	EFF07C
;
	LDA	EFFW2
	BNE	EFF07D
	LDA	#$1F
	STA	SOBENV
	LDA	#$8D
	STA	SOBSWP
	LDA	#32
	STA	SOBFLO
	LDA	#$80
	STA	SOBFHI
	LDA	#4
	STA	EFFW2
	RTS
;
EFF07C:	LDA	EFFW2
	BNE	EFF07D
	LDA	#$DF
	STA	SOBENV
	LDA	#$84
	STA	SOBSWP
	LDA	#32
	STA	SOBFLO
	LDA	#$80
	STA	SOBFHI
	LDA	#8
	STA	EFFW2
;
EFF07D:	DEC	EFFW2
	BNE	EFF07E
	DEC	EFFW1
	BEQ	EFF07F
EFF07E:	RTS
;
EFF07F:	LDA	#0
	STA	EFFCOD
	LDA	#1
	STA	EFFCNT+1
	RTS
;
EF07DT:	DB	1,0,1,1,1,1,1,1,0,1,0,1,1,1,1,1,0,1,1,0
;
;
;
EFS08:	LDA	#$42
	STA	SOBENV
	LDA	#$00
	STA	SOBSWP
	LDA	#$80
	STA	SOBFLO
	LDA	#$08
	STA	SOBFHI
	LDA	#0
	STA	EFFCOD
	LDA	#$10
	STA	EFFCNT+1
	RTS
;
;
;
EFS09:	LDA	#$82
	STA	SOAENV
	STA	SOBENV
	LDA	#$8C
	STA	SOASWP
	LDA	#$84
	STA	SOBSWP
	LDA	#$00
	STA	SOAFLO
	LDA	#$80
	STA	SOBFLO
	LDA	#$09
	STA	SOAFHI
	LDA	#$08
	STA	SOBFHI
	LDA	#0
	STA	EFFCOD
	LDA	#$20
	STA	EFFCNT+1
	RTS
;
EFS0A:	LDA	#255
	STA	EFFCNT+1
	LDA	#$06
	STA	SONENV
	LDA	#$30
	STA	SONLEN
	LDA	#15
	STA	EFFW1
	BNE	EFF0A0
;
EFF0A:	DEC	EFFW2
	BNE	EFF0AB
	DEC	EFFW1
	BMI	EFF0AC
EFF0A0:	LDA	#15
	SEC
	SBC	EFFW1
	STA	SONCLK
	LDA	EFFW1
	ORA	#$10
	STA	SOBENV
	LDA	EFFW1
	AND	#1
	BEQ	EFF0AA
	LDA	#$84
	STA	SOBSWP
	LDA	#$90
	STA	SOBFLO
	LDA	#$08
	STA	SOBFHI
	LDA	#3
	STA	EFFW2
	RTS
EFF0AA:	LDA	#$8C
	STA	SOBSWP
	LDA	#$C0
	STA	SOBFLO
	LDA	#$08
	STA	SOBFHI
	LDA	#3
	STA	EFFW2
EFF0AB:	RTS
EFF0AC:	LDA	#0
	STA	EFFCOD
	LDA	#1
	STA	EFFCNT+1
	RTS
;
;
;
EFS0B:	LDA	#1
	STA	EFFW1
	LDA	#$00
	STA	EFFW2
	LDA	#4
	STA	EFFW3
;
EFF0B:	DEC	EFFW1
	BNE	EFF0BA
	LDA	#$DF
	STA	SOAENV
	LDA	#$9D
	STA	SOASWP
	LDA	#$80
	STA	SOAFLO
	LDA	#$08
	STA	SOAFHI
	LDA	#$20
	STA	EFFW1
	STA	EFFCNT
	STA	EFFCNT+1
	STA	EFFCNT+2
EFF0BA:	CLC
	LDA	EFFW2
	ADC	EFFW3
	STA	EFFW2
	BNE	EFF0BC
	SEC
	LDA	EFFW2
	SBC	EFFW3
	STA	EFFW2
	LDA	#0
	SEC
	SBC	EFFW3
	STA	EFFW3
EFF0BC:	LDA	EFFW2
	AND	#4
	BEQ	EFF0BB
	LDA	#$1F
	STA	SOBENV
	LDA	#$08
	STA	SOBSWP
	LDA	EFFW2
	STA	SOBFLO
	LDA	#$11
	STA	SOBFHI
	RTS
EFF0BB:	LDA	#$10
	STA	SOBENV
	RTS
;
;
;
EFS0C:	LDA	#$04
	STA	SOBENV
	LDA	#$8F
	STA	SOBSWP
	LDA	#$1D
	STA	SOBFLO
	LDA	#$09
	STA	SOBFHI
	LDA	#0
	STA	EFFCOD
	LDA	#$10
	STA	EFFCNT+1
	RTS
;
;
;
EFS0D:	LDA	#33
	STA	EFFW1
	LDA	#255
	STA	EFFCNT
	STA	EFFCNT+1
	STA	EFFCNT+2
;
EFF0D:	DEC	EFFW1
	BEQ	EFF0DE
	LDA	EFFW1
	AND	#7
	BNE	EFF0DA
	LDA	#$80
	STA	SOAFLO
	LDA	#$50
	STA	SOBFLO
	LDA	#$08
	STA	SOAENV
	STA	SOBENV
	STA	SOASWP
	STA	SOBSWP
	STA	SOAFHI
	STA	SOBFHI
EFF0DA:	RTS
;
EFF0DE:	LDA	#$20
	STA	EFFCNT
	STA	EFFCNT+1
	STA	EFFCNT+2
	LDA	#0
	STA	EFFCOD
	RTS
;
;
;
EFS0E:	LDA	#2
	STA	EFFW1
	LDA	#$C0
	STA	EFFW2
;
EFF0E:	LDA	EFFW1
	BEQ	EFF0EE
	LDA	#$DF
	STA	SOAENV
	LDA	#$08
	STA	SOASWP
	LDA	EFFW2
	STA	SOAFLO
	LDA	#$80
	STA	SOAFHI
	LDA	EFFW1
	CMP	#2
	BEQ	EFF0EA
	LDA	EFFW2
	CLC
	ADC	#8
	STA	EFFW2
	CMP	#$F0
	BNE	EFF0EB
EFF0EC:	DEC	EFFW1
EFF0EB:	RTS
;
EFF0EA:	LDA	EFFW2
	SEC
	SBC	#8
	STA	EFFW2
	CMP	#$80
	BNE	EFF0EB
	BEQ	EFF0EC
;
EFF0EE:	LDA	#$DF
	STA	SOAENV
	LDA	#$8C
	STA	SOASWP
	LDA	#$F0
	STA	SOAFLO
	LDA	#$08
	STA	SOAFHI
	LDA	#0
	STA	EFFCOD
	RTS
;
;
;
EFS0F:	LDA	#$5F
	STA	SOBENV
	LDA	#$C4
	STA	SOBSWP
	LDA	#$30
	STA	SOBFLO
	LDA	#$40
	STA	SOBFHI
	STA	EFFW1
	LDA	#$10
	STA	EFFW2
	LDA	#$60
	STA	EFFCNT+1
EFF0FR:	RTS
;
EFF0F:	DEC	EFFW1
	BNE	EFF0FR
	LDA	#8
	STA	EFFW1
	INC	EFFW2
	LDA	EFFW2
	CMP	#$15
	BEQ	EFF0FE
	LDA	#$1F
	STA	SOBENV
	LDA	#$CB
	STA	SOBSWP
	LDA	#$00
	STA	SOBFLO
	LDA	EFFW2
	STA	SOBFHI
	RTS
;
EFF0FE:	LDA	#0
	STA	EFFCOD
	RTS
;
;effect end
;------------------------------------------------------------------------
;
;	H.V.C  SOUND GENERATOR FRQUENCY TABLE.
;
;
FRQTBL:	DW	$0000	; REST
;
;	OCT 3.
;
;	DW	$0357	; C
;	DW	$0327	; C#
;	DW	$02F9	; D
	DW	$02CF	; D#
	DW	$02A6	; E
	DW	$0280	; F
	DW	$025C	; F#
	DW	$023A	; G
	DW	$021A	; G#
	DW	$01FC	; A
	DW	$01DF	; A#
	DW	$01C4	; B
;
;	OCT 4.
;
	DW	$01AB	; C
	DW	$0193	; C#
	DW	$017C	; D
	DW	$0167	; D#
	DW	$0152	; E
	DW	$013F	; F
	DW	$012D	; F#
	DW	$011C	; G
	DW	$010C	; G#
	DW	$00FD	; A
	DW	$00EE	; A#
	DW	$00E1	; B
;
;	OCT 5.
;
	DW	$00D4	; C
	DW	$00C8	; C#
	DW	$00BD	; D
	DW	$00B2	; D#
	DW	$00A8	; E
	DW	$009F	; F
	DW	$0096	; F#
	DW	$008D	; G
	DW	$0085	; G#
	DW	$007E	; A
	DW	$0076	; A#
	DW	$0070	; B
;
;	OCT 6.
;
	DW	$0069	; C
	DW	$0063	; C#
	DW	$005E	; D
	DW	$0058	; D#
	DW	$0053	; E
	DW	$004F	; F
;	DW	$004A	; F#
;	DW	$0046	; G
;	DW	$0042	; G#
;	DW	$003E	; A
;
;
;
;
;
MCTMPO	MACRO	%1
	ENDM
;
MCREST	MACRO
	DB	0
	ENDM
;
MCTONE	MACRO	%1,%2
	DB	(%1-3)*12+%2+1-3
	ENDM
;
MCLENG	MACRO	%1
	DB	%1+$80
	ENDM
;
MCEND	MACRO
	DB	$FF
	ENDM
;
MCDC	MACRO
	DB	$FE
	ENDM
;
MCRPB	MACRO	%1
	DB	$FD,%1
	ENDM
;
MCRPE	MACRO
	DB	$FC
	ENDM
;
MCTAI	MACRO
	DB	$FB
	ENDM
;
MCTAIE	MACRO
	DB	$FA
	ENDM
;
MCLEN	MACRO	%1
	ENDM
;
DATATP:
MTBL1:	DW	MTOJOB
	DW	MTOJOC
	DW	MTOJOA
	DB	$80,$80
;
MTBL2:	DW	MBGMB
	DW	MBGMC
	DW	MBGMA
	DB	$80,$40
;
MTBL3:	DW	MFIREB
	DW	MFIREC
	DW	MFIREA
	DB	$40,$40
;
MTBL4:	DW	MCATB
	DW	MCATC
	DW	MCATA
	DB	$00,$00
;
MTBL5:	DW	MCLRB
	DW	MCLRC
	DW	MCLRA
	DB	$80,$80
;
MTBL6:	DW	MCOMPB
	DW	MCOMPC
	DW	MCOMPA
	DB	$80,$00
;
MTBL8:	DW	MOVERB
	DW	MOVERC
	DW	MOVERA
	DB	$80,$80
;
MTBL9:	DW	MBONUA
	DW	MBONUB
	DW	MBONUC
	DB	$80,$80
;
MTBLA:	DW	MWARPB
	DW	MWARPC
	DW	MWARPA
	DB	$80,$00

